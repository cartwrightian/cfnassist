

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

ext.currentGitSha = { ->
    def result = new ByteArrayOutputStream()
    exec {
        commandLine 'git'
        args 'rev-parse', 'HEAD'
        standardOutput = result
    }
    return result.toString().replaceAll("(?:\\n|\\r)", "")
}

def candidate = System.getenv("CIRCLE_BUILD_NUM") ?: "0"
def buildNumber = System.getenv("RELEASE_NUMBER") ?: candidate

def githubToken = System.getenv("GITHUB_TOKEN")
def gitUser = System.getenv("CIRCLE_PROJECT_USERNAME") ?: System.getenv("GIT_USERNAME")
def gitProject = System.getenv("CIRCLE_PROJECT_REPONAME") ?: System.getenv("GIT_PROJECT")
def gitSha = System.getenv("CIRCLE_SHA1") ?: currentGitSha()
def goPath = System.getenv("GOPATH") ?: "../go"

version = '1.3.'+buildNumber

repositories {
    mavenCentral()
}

project.ext {
    awsSdk = '2.20.160'
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.17',
            'ch.qos.logback:logback-classic:1.5.17'
            'ch.qos.logback:logback-core:1.5.17'

    implementation 'software.amazon.awssdk:protocol-core:'+awsSdk,
            'software.amazon.awssdk:aws-query-protocol:'+awsSdk,
            'software.amazon.awssdk:ec2:'+awsSdk,
            'software.amazon.awssdk:cloudformation:'+awsSdk,
            'software.amazon.awssdk:elasticloadbalancing:'+awsSdk,
            'software.amazon.awssdk:elasticloadbalancingv2:'+awsSdk,
            'software.amazon.awssdk:sns:'+awsSdk,
            'software.amazon.awssdk:sqs:'+awsSdk,
            'software.amazon.awssdk:iam:'+awsSdk,
            'software.amazon.awssdk:rds:'+awsSdk,
            'software.amazon.awssdk:s3:'+awsSdk,
            'software.amazon.awssdk:cloudwatchlogs:'+awsSdk

    implementation 'commons-io:commons-io:2.14.0',
            'commons-cli:commons-cli:1.5.0',
            'commons-net:commons-net:3.9.0',
            'org.apache.ant:ant:1.10.13',
            'com.fasterxml.jackson.core:jackson-core:2.18.3',
            'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1',
            'org.junit.platform:junit-platform-launcher:1.12.1',
            'org.easymock:easymock:5.2.0'

}

application {
    mainClass = 'tw.com.commandline.Main'
}

jar {
    manifest {
        attributes 'Main-Class': 'tw.com.commandline.Main'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources.srcDir file('test/resources')
    }
}


tasks.register('unit', Test) {
    dependsOn compileJava
    useJUnitPlatform()
    environment 'testEnvVar', 'testValue'
    filter {
        includeTestsMatching "tw.com.unit.*"
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

tasks.register('integration', Test) {
    dependsOn compileJava
    useJUnitPlatform()
    filter {
        includeTestsMatching "tw.com.integration.*"
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

tasks.register('acceptance', Test) {
    dependsOn compileJava
    useJUnitPlatform()
    filter {
        includeTestsMatching "tw.com.acceptance.*"
    }
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

distributions {
    main {
        contents {
            from { 'README.md' }
            into('conf') {
                from 'conf'
            }
        }
    }
}

tasks.register('release', Exec) {
    dependsOn distZip
    executable goPath + '/bin/ghr'
    //args '-t', githubToken, '-u', gitUser, '-r', gitProject, '-c', gitSha, '-delete', version, './build/distributions'
}

tasks.register('copyDependencies', Copy) {
    from configurations.default
    into 'build/dependencies'
}


